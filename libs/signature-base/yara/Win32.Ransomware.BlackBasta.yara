rule Win32_Ransomware_BlackBasta : tc_detection malicious
{
    meta:

        author              = "ReversingLabs"

        source              = "ReversingLabs"
        status              = "RELEASED"
        sharing             = "TLP:WHITE"
        category            = "MALWARE"
        malware             = "BLACKBASTA"
        description         = "Yara rule that detects BlackBasta ransomware."

        tc_detection_type   = "Ransomware"
        tc_detection_name   = "BlackBasta"
        tc_detection_factor = 5

    strings:

        $find_files = {
            53 50 FF 15 ?? ?? ?? ?? 8B F0 83 FE ?? 75 ?? FF B5 ?? ?? ?? ?? 53 53 57 E8 ?? ?? ??
            ?? 83 C4 ?? 8B F0 38 9D ?? ?? ?? ?? 74 ?? FF B5 ?? ?? ?? ?? E8 ?? ?? ?? ?? 59 8B C6
            E9 ?? ?? ?? ?? 8B 8D ?? ?? ?? ?? 8B 41 ?? 2B 01 C1 F8 ?? 89 85 ?? ?? ?? ?? 89 9D ??
            ?? ?? ?? 89 9D ?? ?? ?? ?? 89 9D ?? ?? ?? ?? 89 9D ?? ?? ?? ?? 89 9D ?? ?? ?? ?? 88
            9D ?? ?? ?? ?? E8 ?? ?? ?? ?? 50 8D 85 ?? ?? ?? ?? 50 8D 85 ?? ?? ?? ?? 50 8D 85 ??
            ?? ?? ?? 50 E8 ?? ?? ?? ?? 83 C4 ?? F7 D8 1B C0 F7 D0 23 85 ?? ?? ?? ?? 80 38 ?? 75
            ?? 8A 48 ?? 84 C9 74 ?? 80 F9 ?? 75 ?? 38 58 ?? 74 ?? FF B5 ?? ?? ?? ?? FF B5 ?? ??
            ?? ?? 57 50 E8 ?? ?? ?? ?? 83 C4 ?? 89 85 ?? ?? ?? ?? 85 C0 75 ?? 38 9D ?? ?? ?? ??
            74 ?? FF B5 ?? ?? ?? ?? E8 ?? ?? ?? ?? 59 8D 85 ?? ?? ?? ?? 50 56 FF 15
        }

        $encrypt_files = {
            55 8B EC 51 8B 45 ?? 53 56 57 8B F9 8B 5F ?? 3B C3 77 ?? 89 7D ?? 83 FB ?? 72 ?? 8B
            0F 89 4D ?? 8D 34 00 89 47 ?? 56 FF 75 ?? 51 E8 ?? ?? ?? ?? 8B 45 ?? 83 C4 ?? 33 C9
            66 89 0C 06 8B C7 5F 5E 5B 8B E5 5D C2 ?? ?? 3D ?? ?? ?? ?? 0F 87 ?? ?? ?? ?? 8B F0
            83 CE ?? 81 FE ?? ?? ?? ?? 76 ?? BE ?? ?? ?? ?? EB ?? 8B CB B8 ?? ?? ?? ?? D1 E9 2B
            C1 3B D8 76 ?? BE ?? ?? ?? ?? EB ?? 8D 04 19 3B F0 0F 42 F0 8D 46 ?? 8B CF 50 E8 ??
            ?? ?? ?? 8B 4D ?? 89 77 ?? 89 45 ?? 89 4F ?? 8D 34 09 56 FF 75 ?? 50 E8 ?? ?? ?? ??
            8B 45 ?? 33 C9 83 C4 ?? 66 89 0C 06 83 FB ?? 72 ?? 8B 07 8D 1C 5D ?? ?? ?? ?? 81 FB
            ?? ?? ?? ?? 72 ?? 8B 48 ?? 83 C3 ?? 2B C1 83 C0 ?? 83 F8 ?? 77 ?? 8B C1 53 50 E8 ??
            ?? ?? ?? 8B 45 ?? 83 C4 ?? 89 07 8B C7 5F 5E 5B 8B E5 5D C2
        }

        $delete_shadow_copies = {
            68 ?? ?? ?? ?? 6A ?? 6A ?? 6A ?? 6A ?? 6A ?? 6A ?? 6A ?? 6A ?? 6A ?? 8D 45 ?? 50 FF
            15 ?? ?? ?? ?? FF 15 ?? ?? ?? ?? A3 ?? ?? ?? ?? FF 15 ?? ?? ?? ?? 68 ?? ?? ?? ?? E8
            ?? ?? ?? ?? 68 ?? ?? ?? ?? E8 ?? ?? ?? ?? C7 45 ?? ?? ?? ?? ?? C7 45 ?? ?? ?? ?? ??
            C7 45 ?? ?? ?? ?? ?? C7 45 ?? ?? ?? ?? ?? C7 45 ?? ?? ?? ?? ?? C6 45 ?? ?? C7 45 ??
            ?? ?? ?? ?? C7 45 ?? ?? ?? ?? ?? C6 45 ?? ?? C7 45 ?? ?? ?? ?? ?? 6A ?? 8D 45 ?? 50
            8D 45 ?? 50 E8 ?? ?? ?? ?? 83 C4 ?? 8B F0 C6 45 ?? ?? 81 FE ?? ?? ?? ?? 74 ?? B9 ??
            ?? ?? ?? E8
        }

    condition:
        uint16(0) == 0x5A4D and
        (
            $find_files
        ) and
        (
            $encrypt_files
        ) and
        (
            $delete_shadow_copies
        )
}